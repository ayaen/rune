//  Copyright 2023 Google LLC.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

// This should replace the Line class in the database.  For now, it is local to
// the parser generator.

use filepath

class Location(self, filepath: Filepath, pos:u32, len: u32, line: u32) {
  self.pos = pos
  self.len = len
  self.line = line
  filepath.appendLocation(self)

  // Write the line to stdout, with a filename and line number prefix.
  func dump(self) {
    filepath = self.filepath
    assert !isnull(filepath)
    path = filepath!.getRelativePath()
    if path.length() > 0 {
      println "File %s, line %u:" % (path, self.line)
    }
    if filepath!.text.length() > 0 {
      self.printLineAndCarots()
    }
  }

  // Like dump(), but prints msg.
  func error(self, msg) {
    filepath = self.filepath
    assert !isnull(filepath)
    path = filepath!.getRelativePath()
    if path.length() > 0 {
      println "File %s, line %u: %s" % (path, self.line, msg)
    }
    if filepath!.text.length() > 0 {
      self.printLineAndCarots()
    }
  }

  func printLineAndCarots(self) {
    start = self.findLineStart()
    end = self.findLineEnd()
    println self.filepath!.text[start:end]
    for i in range(self.pos - start) {
      print " "
    }
    for i in range(self.len) {
      print "^"
    }
    println
  }

  func findLineStart(self) -> u32 {
    pos = self.pos
    while pos != 0 {
      pos -= 1
      c = self.filepath!.text[pos]
      if c == '\n' {
        return pos + 1
      }
    }
    return 0u32
  }

  func findLineEnd(self) -> u32 {
    pos = self.pos
    while pos < <pos>self.filepath!.text.length() {
      if self.filepath!.text[pos] == '\n' {
        return pos
      }
      pos += 1
    }
    return <pos>self.filepath!.text.length()
  }
}

relation DoublyLinked Filepath Location

unittest {
  rootFilepath = Filepath.new("testdata", null(Filepath), false)
  filepath = Filepath.new("testdata/hello", rootFilepath, false)
  location1 = Location(filepath, 0u32, 5u32, 1u32)
  location2 = Location(filepath, 7u32, 6u32, 1u32)
  filepath.readFile()
  location1.dump()
  location2.dump()
}
